// SPDX-License-Identifier: Unlicensed
pragma solidity ^0.8.13;

import {AccessControlStorage} from "./AccessControlStorage.sol";
import {IAccessControlInternal} from "./IAccessControlInternal.sol";
import {UintUtils} from "@solidstate/contracts/utils/UintUtils.sol";
import {AddressUtils} from "@solidstate/contracts/utils/AddressUtils.sol";

contract AccessControlInternal is IAccessControlInternal {
    using AccessControlStorage for AccessControlStorage.Storage;
    using AddressUtils for address;
    using UintUtils for uint256;

    bytes32 internal constant DEFAULT_ADMIN_ROLE = 0x00;

    /**
     * @notice Modifier that checks that an account has a specific role. Reverts
     * with a standardized message including the required role.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     *
     */
    modifier onlyRole(bytes32 role) {
        _checkRole(role);
        _;
    }

    /**
     * @notice query if account's has role
     * @param role role to query
     * @param account account to query
     * @dev Returns `true` if `account` has been granted `role`.
     */
    function _hasRole(bytes32 role, address account)
        internal
        view
        virtual
        returns (bool)
    {
        return AccessControlStorage.getStorage().roles[role].members[account];
    }

    /**
     * @notice query role by msg.sender
     * @param role role to query
     * @dev Revert with a standard message if `msg.sender` is missing `role`.
     * Overriding this function changes the behavior of the {onlyRole} modifier.
     *
     * Format of the revert message is described in {_checkRole}.
     *
     */
    function _checkRole(bytes32 role) internal view virtual {
        _checkRole(role, msg.sender);
    }

    /**
     * @notice query role by account
     * @param role role to query
     * @param account account to query
     * @dev Revert with a standard message if `account` is missing `role`.
     *
     * The format of the revert reason is given by the following regular expression:
     *
     *  /^AccessControl: account (0x[0-9a-f]{40}) is missing role (0x[0-9a-f]{64})$/
     */
    function _checkRole(bytes32 role, address account) internal view virtual {
        if (!_hasRole(role, account)) {
            revert(
                string(
                    abi.encodePacked(
                        "AccessControl: account ",
                        account.toString(),
                        " is missing role ",
                        uint256(role).toHexString(32)
                    )
                )
            );
        }
    }

    /**
     * @notice query admin role
     * @param role role to query
     * @dev Returns the admin role that controls `role`. See {grantRole} and
     * {revokeRole}.
     *
     * To change a role's admin, use {_setRoleAdmin}.
     */
    function _getRoleAdmin(bytes32 role)
        internal
        view
        virtual
        returns (bytes32)
    {
        return AccessControlStorage.getStorage().roles[role].adminRole;
    }

    /**
     * @notice set role as admin role
     * @param role role to set
     * @param adminRole admin role to set
     * @dev Sets `adminRole` as ``role``'s admin role.
     *
     * Emits a {RoleAdminChanged} event.
     */
    function _setRoleAdmin(bytes32 role, bytes32 adminRole) internal virtual {
        bytes32 previousAdminRole = _getRoleAdmin(role);
        AccessControlStorage.getStorage().roles[role].adminRole = adminRole;
        emit RoleAdminChanged(role, previousAdminRole, adminRole);
    }

    /**
     * @notice grant role to account
     * @param role role to grant
     * @param account account to grant the role
     * @dev Grants `role` to `account`.
     *
     *  * If `account` had not been already granted `role`, emits a {RoleGranted}
     * event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     *
     * Internal function without access restriction.
     */
    function _grantRole(bytes32 role, address account) internal virtual {
        if (!_hasRole(role, account)) {
            AccessControlStorage.getStorage().roles[role].members[
                account
            ] = true;
            emit RoleGranted(role, account, msg.sender);
        }
    }

    /**
     * @notice revoke role from account
     * @param role role to revoke
     * @param account account to revoke the role
     * @dev Revokes `role` from `account`.
     *
     * If `account` had been granted `role`, emits a {RoleRevoked} event.
     *
     * Requirements:
     *
     * - the caller must have ``role``'s admin role.
     *
     * Internal function without access restriction.
     */
    function _revokeRole(bytes32 role, address account) internal virtual {
        if (_hasRole(role, account)) {
            AccessControlStorage.getStorage().roles[role].members[
                account
            ] = false;
            emit RoleRevoked(role, account, msg.sender);
        }
    }
}
